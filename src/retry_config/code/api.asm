;===============================================================================
; Routines that can be called by external UberASM files
;===============================================================================

;===============================================================================
; Routine to make the player respawn in the current level at the last
; checkpoint. This has the same effect as dying and hitting Retry, or dying with
; instant Retry enabled, but skipping everything related to death (animation,
; music, prompt, life loss, etc.).
;
; Inputs: N/A
; Outputs: N/A
; Pre: A/X/Y 8 bits
; Post: A/X/Y 8 bits
; Example: JSL retry_api_respawn_in_level
;===============================================================================
respawn_in_level:
    jsl in_level_main_dying_respawn
    jsr in_level_death_routine_no_game_over
    rtl

;===============================================================================
; Routine to save the game, which will also save the addresses defined in the
; "sram_tables.asm" file (including those under ".global").
;
; Inputs: N/A
; Outputs: N/A
; Pre: N/A
; Post: A/X/Y 8 bits, DB/X/Y preserved
; Example: JSL retry_api_save_game
;===============================================================================
save_game:
    jsr shared_save_game
    rtl

;===============================================================================
; Routine to save the global variables to SRAM, meaning just the addresses found
; under ".global" in the "sram_tables.asm" file.
; This can be useful if you need to update the global variables outside of a
; save file (e.g. on the title screen) or if you want to save them without
; saving the file specific addresses.
;
; Inputs: N/A
; Outputs: N/A
; Pre: N/A
; Post: A clobbered, DB/X/Y/P preserved, $02-$0B clobbered
; Example: JSL retry_api_save_global_variables
;===============================================================================
save_global_variables:
if !sram_feature
    ; Preserve DB, X, Y, P.
    phb : phk : plb
    phx : phy : php
    
    ; Call the actual routine
    jsr sram_save_global
    
    ; Restore DBR, P, X and Y.
    plp : ply : plx
    plb
endif
    rtl

;===============================================================================
; Routine to remove the current level's checkpoint, meaning entering it again
; will load the main sublevel's entrance.
; Note: this can be also called outside of a level if you want to reset a
; specific level's checkpoint. Just make sure that $13BF has the level number
; you want before calling this.
;
; Inputs: N/A
; Outputs: N/A
; Pre: A/X/Y 8 bits
; Post: A/X/Y 8 bits, DB/X/Y preserved
; Example: JSL retry_api_reset_level_checkpoint
;===============================================================================
reset_level_checkpoint:
    jsr shared_reset_checkpoint
    rtl

;===============================================================================
; Routine to remove all levels checkpoints, effectively resetting their state
; as if it were a new game. If you're saving the CPs to SRAM, you'll need to
; call the save game routine afterwards to also reset the CPs state in SRAM.
;
; Inputs: N/A
; Outputs: N/A
; Pre: N/A
; Post: A/X/Y size preserved, DB/X/Y preserved
; Example: JSL retry_api_reset_all_checkpoints
;===============================================================================
reset_all_checkpoints:
    ; A/X/Y 8 bits
    phx : phy : php
    sep #$30

    ; Initialize the checkpoint ram table.
    ldx #$BE
    ldy #$5F
-   tya : cmp #$25 : bcc +
    clc : adc #$DC
+   sta !ram_checkpoint,x
    lda #$00 : adc #$00 : sta !ram_checkpoint+1,x
    lda $1EA2|!addr,y : and #~$40 : sta $1EA2|!addr,y
    dex #2
    dey : bpl -

    ; Initialize respawn RAM in case it's called inside a level.
    %lda_13BF() : asl : tax
    rep #$20
    lda !ram_checkpoint,x : sta !ram_respawn

    ; Initialize "set checkpoint" handle to $FFFF.
    lda #$FFFF : sta !ram_set_checkpoint

    ; Restore X/Y/P
    plp : ply : plx
    rtl

;===============================================================================
; Routine to configure which tiles will be used by the sprite status bar.
; You can configure these elements: item box, timer, coin counter, lives
; counter, bonus stars counter and death counter. Each element needs a 16x16
; sprite tile to be reserved.
; This routine should be called in UberASM level init code, to overwrite the
; default settings from "settings_global.asm", in case you want to hide some or
; all of the elements in some level or if you need to change their tile or
; palette.
; Each element needs a 16 bit value that determines which 16x16 tile it will use
; and what palette to use. The first digit is the palette row to use (8-F),
; while the other 3 digits are the tile number (000-1FF). If an element is set
; to $0000, it won't be displayed.
; For the coin counter, you can add $0200 to the value to only display dragon
; coins, or add $0400 to only display coins.
; Note: for a more convenient way to configure the sprite status bar, you can
;       use the %ssb_config commands in "settings_local.asm".
;
; Inputs: for each item, in order, you write the value after the JSL in the
;         format described above (see example).
; Outputs: N/A
; Pre: N/A
; Post: A/X/Y 8 bit and clobbered, DB preserved
; Example:
;     JSL retry_api_configure_sprite_status_bar
;     dw $B080 ; Item box: palette B, tile 0x80
;     dw $8088 ; Timer: palette 8, tile 0x88
;     dw $80C2 ; Coin counter: palette 8, tile 0xC2
;     dw $0000 ; Lives counter: hidden
;     dw $0000 ; Bonus stars counter: hidden
;     dw $0000 ; Death counter: hidden
;     ... <- your code will continue here after the JSL
;===============================================================================
configure_sprite_status_bar:
if !sprite_status_bar
    phb
    ; Set DBR equal to caller routine bank
    sep #$30
    lda $04,s : pha : plb
    ; Use Y to read address after the routine call
    rep #$30
    lda $02,s : tay
    ; Copy the values from after the JSL to sprite status bar ram
    lda $0001,y : and #$7FFF : sta !ram_status_bar_item_box_tile
    lda $0003,y : and #$7FFF : sta !ram_status_bar_timer_tile
    lda $0005,y : and #$7FFF : sta !ram_status_bar_coins_tile
    lda $0007,y : and #$7FFF : sta !ram_status_bar_lives_tile
    lda $0009,y : and #$7FFF : sta !ram_status_bar_bonus_stars_tile
    lda $000B,y : and #$7FFF : sta !ram_status_bar_death_tile
    plb
endif
    ; Make sure the code returns at the right place
    rep #$20
    lda $01,s : clc : adc.w #2*!ssb_elements_number : sta $01,s
    sep #$30
    rtl

;===============================================================================
; Routine to hide the status bar for the current level. This routine should be
; called in UberASM level init code.
; Note: for a more convenient way to hide the sprite status bar, you can use the
;       %ssb_hide commands in "settings_local.asm".
;
; Inputs: N/A
; Outputs: N/A
; Pre: N/A
; Post: A/X/Y 8 bit and clobbered, DB preserved
; Example:
;     JSL retry_api_hide_sprite_status_bar
;===============================================================================
hide_sprite_status_bar:
if !sprite_status_bar
    jsl configure_sprite_status_bar : %dwn(0,!ssb_elements_number)
endif
    rtl

;===============================================================================
; Routine to get the current Retry type, i.e. if currently the level is set to
; have Retry prompt, instant Retry or no Retry.
; The returned value has this format:
; - $01 = Retry prompt enabled & play the death song when the player dies
; - $02 = Retry prompt enabled & play only the death sfx when the player dies
; - $03 = instant Retry enabled & play only the death sfx when the player dies
; - $04 = instant Retry enabled & play the death song when the player dies
; - $05 = Retry disabled (vanilla death)
;
; Inputs: N/A
; Outputs: A = Retry type
; Pre: A 8 bits
; Post: A/X/Y size preserved, DB/X/Y preserved
; Example: JSL retry_api_get_retry_type
;===============================================================================
get_retry_type:
    jsr shared_get_prompt_type
    rtl

;===============================================================================
; Routine to get check if a save file is empty.
;
; Inputs: save file to check in $010A|!addr (0 = save file 1, 1 = save file
;         2, 2 = save file 3, behavior undefined for other values)
; Outputs: Carry set = save file empty
;          Carry clear = save file not empty
; Pre: A 8 bits
; Post: A/X/Y 8 bit and clobbered, DB preserved
; Example:
;         LDA #$02 ; Check save file 3
;         STA $010A|!addr
;         JSL retry_api_is_save_file_empty
;         BCS empty
;     not_empty:
;         ...
;     empty:
;         ...
;===============================================================================
is_save_file_empty:
    sep #$30
    ldx $010A|!addr
    phb
    ; Can't use %jsl_to_rts_db because plb clobbers the Z flag
    lda.b #$00|!bank8 : pha : plb
    %jsl_to_rts($009DB5)
    sep #$30
    bne .empty
.not_empty:
    plb
    clc
    rtl
.empty:
    plb
    sec
    rtl

;===============================================================================
; Routine to get the address in SRAM for a specific variable.
; By "variable" it's meant any of the RAM addresses that are saved to SRAM
; specified in the sram save table.
; If not searching for a variable under ".global", the variable will be searched
; in the currently loaded save file. If calling this before a save file is
; loaded (e.g. the title screen), you need to specify the save file to look into
; by setting the $010A|!addr address (0 = save file 1, 1 = save file 2, 2 = save
; file 3, behavior undefined for other values).
; This could be useful to read/write values in SRAM directly, for example if you
; need to update some SRAM value without the game being saved, or if you need to
; display something on the title screen depending on the save file data.
; Note: this will always return "variable not found" if !sram_feature = 0 or if
; the chosen save file is empty (for local variables).
;
; Inputs: variable address to search for in ROM right after the JSL
;         This means the call should look like this:
;             JSL retry_api_get_sram_variable_address
;             dl <variable address>
;             ... <- your code will continue here after the JSL
; Outputs: Carry set = variable not found
;          Carry clear = variable found -> SRAM address stored in $00-$02
;            In this case the value in SRAM can be accessed indirectly with the
;            LDA/STA [$00] and LDA/STA [$00],y instructions.
; Pre: N/A
; Post: A/X/Y 8 bit and clobbered, DB preserved
; Example 1 (file already loaded):
;         JSL retry_api_get_sram_variable_address
;         dl retry_ram_death_counter ; Variable to search for
;         BCS error
;     found:
;         LDY #$01
;         LDA #$09
;         STA [$00],y ; Set second death counter digit in SRAM to 9
;     error:
;         ...
;
; Example 2 (on title screen):
;         LDA #$01 ; $01 = search in save file 2
;         STA $010A|!addr
;         JSL retry_api_get_sram_variable_address
;         dl retry_ram_death_counter ; Variable to search for
;         BCS empty_file
;         LDA [$00] ; Get first death counter digit of save file 2
;         ...
;     empty_file:
;         ...
;===============================================================================
get_sram_variable_address:
if !sram_feature
    phb
    ; Set DBR equal to caller routine bank
    sep #$30
    lda $04,s : pha : plb
    ; Use Y to read address after the routine call
    rep #$30
    lda $02,s : tay
    stz $00
    ldx #$0000
.loop:
    ; Check if the current SRAM variables corresponds to the input
    lda.l sram_tables_save+0,x : cmp $0001,y : bne .next
    lda.l sram_tables_save+1,x : cmp $0002,y : bne .next
.found:
    ; Check if it's a local or global variable
    cpx.w #sram_tables_save_global-sram_tables_save : bcc ..local
..global:
    ; If global, get the global SRAM address
    jsr sram_get_global_sram_addr
    bra ..shared
..local:
    ; If local and the save file is empty, return not found
    jsl is_save_file_empty : bcs .not_found
    ; Otherwise, get the save file SRAM address
    jsr sram_get_file_sram_addr
..shared:
    ; Add the calculated offset to the SRAM address
    clc : adc $00 : sta $00
    sep #$20
    lda.b #!sram_addr>>16 : sta $02
    ; Clear carry (address found)
    clc
    bra .return
.next:
    ; Update the SRAM offset with the current variable size
    lda.l sram_tables_save+3,x : clc : adc $00 : sta $00
    ; Go to the next SRAM variable
    txa : clc : adc #$0005
    ; If at the end of the SRAM table, end
    cmp.w #sram_tables_sram_defaults-sram_tables_save : bcs .not_found
    ; Check if we reached the global variables part
    tax
    cpx.w #sram_tables_save_global-sram_tables_save : bne .loop
    ; If so, reset the offset
    stz $00
    bra .loop
.not_found:
    ; Set carry (address not found)
    sec
.return:
    plb
    ; Make sure the code returns at the right place
    rep #$20
    lda $01,s : inc #3 : sta $01,s
    sep #$30
    rtl
else
    ; Make sure the code returns at the right place
    rep #$20
    lda $01,s : clc : adc #$0003 : sta $01,s
    ; Set carry (address not found)
    sep #$31
endif
    rtl
